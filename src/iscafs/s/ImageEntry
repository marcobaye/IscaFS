#multipass on
; ImageEntry
; ~~~~~~~~~~
; ImageFS function entry points.
; 23 Mar 2004: 32bitclean


.IE_Args
	; handle supported reason codes
	TEQ	r0,#4
	BEQ	Args_ReadAllocatedSize
	TEQ	r0,#9
	BEQ	Args_ReadDateStamp
	; handle unsupported reason codes
	TEQ	r0,#3			; WriteFileExtent
	TEQNE	r0,#6			; NotifyOfFlush
	TEQNE	r0,#7			; EnsureFileSize
	TEQNE	r0,#8			; WriteZerosToFile
	BEQ	IE_Error_ReadOnly
	; handle unknown reason codes - maybe RO 7 adds a new one? ;)
	B	IE_Error_Unsupported


.IE_File
	; handle unsupported reason codes
	TEQ	r0,#0			; save file
#if config_write%=0
	TEQNE	r0,#1			; write catalogue information
#endif
	TEQNE	r0,#6			; delete object
	TEQNE	r0,#7			; create file
	TEQNE	r0,#8			; create directory
	BEQ	IE_Error_ReadOnly
	; handle supported reason codes
#if config_write%
	TEQ	r0,#1
	BEQ	File_WriteCatalogueInfo
#endif
	TEQ	r0,#5
	BEQ	File_ReadCatalogueInfo
	TEQ	r0,#10
	;BEQ	File_ReadBlockSize
	BNE	IE_Error_Unsupported	; handle unknown reason codes
;.File_ReadBlockSize
	MOV	r2,#1024
	CMP	R0,R0			; clears V
	MOV	pc,r14


.IE_Func
; changed ordering. New order speeds up directory lookups and
; facilitates adding the missing entry points. I blatantly copied MW's
; optimisation technique and only adjusted the numbers.
	TEQ	r0,#14			; 14 = "ReadDirEntries"
	TEQNE	r0,#15			; 15 = "ReadDirEntriesAndInfo"
	BEQ	Directory_ReadEntries
	TEQ	r0,#8			;  8 = "Rename"
	BEQ	IE_Error_ReadOnly
	; now we'll have to handle an interval:
	; <21	invalid for image filing systems
	; 21-37	mostly valid (17 entries)
	; >37	invalid (may be added in later versions of RISC OS ;))
	SUBS	r0, r0, #21		;P very nice optimisation here ;-)
	RSBGES	r0, r0, #16
	ADDGE	pc, pc, r0, LSL #2
	B	IE_Error_Unsupported	; >37 or <21
	B	IE_Error_ReadOnly	; 37 AddDefect64
	B	IE_Error_Unsupported	; 36 ReadDefectList64
	B	Image_ReadFreeSpace64	; 35 ReadFreeSpace64
	B	IE_Error_Unsupported	; 34 illegal for image filing systems
	B	IE_Error_Unsupported	; 33 GetUsageOfOffset
	B	IE_Error_ReadOnly	; 32 StampImage
	B	IE_Error_ReadOnly	; 31 NameImage
	B	Image_ReadFreeSpace	; 30 ReadFreeSpace
	B	Image_ReadUsedSpaceMap	; 29 ReadUsedSpaceMap
	B	IE_Error_ReadOnly	; 28 WriteBootOption
	B	Image_ReadBootOption	; 27 ReadBootOption
	B	IE_Error_ReadOnly	; 26 AddDefect
	B	IE_Error_Unsupported	; 25 ReadDefectList29
	B	IE_Error_Unsupported	; 24 illegal for image filing systems
	B	IE_Error_Unsupported	; 23 illegal for image filing systems
	B	Image_Umount		; 22 CloseImage
	B	Image_Mount		; 21 OpenImage


.IE_Error_ReadOnly
	ADD	r0,pc,#0
	B	SetVFlagAndMovPcR14
	EQUD	0
	EQUS	"Filing system is read-only."+CHR$0:FNalign


.IE_Error_Unsupported
	ADD	r0,pc,#0
	B	SetVFlagAndMovPcR14
	EQUD	0
	EQUS	"This operation is not supported by IscaFS."+CHR$0:FNalign
